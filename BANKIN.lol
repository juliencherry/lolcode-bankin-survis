HAI 1.2
    CAN HAS STDIO?
    I HAS A MONIES ITZ 0.00
    HOW IZ I GETTINMONIES
        FOUND YR MONIES
    IF U SAY SO
    HOW IZ I ADDIN YR MOARMONIES
        MONIES R SUM OF MONIES AN MOARMONIES
        FOUND YR MONIES
    IF U SAY SO
    HOW IZ I TAKIN YR MOARMONIES
        MONIES R DIFF OF MONIES AN MOARMONIES
        FOUND YR MONIES
    IF U SAY SO
    VISIBLE "HAI DIS IZ LOLCAT BANKIN SURVIS. WHAT IZ YR NAEM? "!
    I HAS A NAEM
    GIMMEH NAEM
    VISIBLE "HAI THERE :{NAEM}. U WANT MONIES? :)"
    VISIBLE "ENTUR 1 FUR GETTIN MONIES"
    VISIBLE "ENTUR 2 FUR ADDIN MONIES"
    VISIBLE "ENTUR 3 FUR TAKIN MONIES"
    VISIBLE "ENTUR ANYTHIN ELSE AN GTFO :)"
    I HAS A AWSUM ITZ WIN
    IM IN YR ACOWNT
        AWSUM, O RLY?
            YA RLY
                VISIBLE "WHAT WUD U LAIK TO DO? "!
                I HAS A ACSHUN
                GIMMEH ACSHUN
                ACSHUN, WTF?
                    OMG "1"
                        VISIBLE "U HAS $"...
                        I IZ GETTINMONIES MKAY
                        GTFO
                    OMG "2"
                        VISIBLE "HOW MUCH MONIES U WANNA ADD? "!
                        I HAS A MOARMONIES
                        GIMMEH MOARMONIES
                        VISIBLE "NOW U HAS $"...
                        I IZ ADDIN YR MOARMONIES MKAY
                        GTFO
                    OMG "3"
                        VISIBLE "HOW MUCH MONIES U WANNA TAKE? "!
                        I HAS A MOARMONIES
                        GIMMEH MOARMONIES
                        VISIBLE "NOW U HAS $"...
                        I IZ TAKIN YR MOARMONIES MKAY
                        GTFO
                    OMGWTF
                        VISIBLE "THX FUR USIN LOLCAT BANKIN SURVIS"
                        AWSUM R FAIL
                OIC
            NO WAI, GTFO
        OIC
    IM OUTTA YR ACOWNT
KTHXBYE